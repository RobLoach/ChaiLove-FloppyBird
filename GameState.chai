class GameState {

	def GameState() {
		this.pause = false
		this.pauseText = "PAUSE"
		this.pauseWidth = assets["font-pixel"].getWidth(this.pauseText)
		this.pauseHeight = assets["font-pixel"].getHeight(this.pauseText)
	}

	def enter() {
		assets["music1"].play()
		assets["music2"].stop()
		assets["music3"].stop()
	}

	def exit() {
		// Nothing.
	}

	def update(delta) {
		// Check if the game is paused.
		if (this.pause) {
			return
		}

		// Update the bird position, resetting if needed.
		if (bird.update(delta) == false) {
			states.set("menu")
			return
		}

		// Update all pipes.
		var removePipe = 0
		for (pipe : pipes) {
			// Check collisions with any pipes.
			if (pipe.collide(bird)) {
				assets["hit"].play()
				states.set("menu")
				return
			}

			// Update will return false if we are to remove it.
			if (pipe.update(delta, bird.speed) == false) {
				removePipe = removePipe + 1
			}

			// Count the score.
			if (bird.x > pipe.x + pipe.width / 2 && pipe.top && pipe.scored == false) {
				addScore()
				pipe.scored = true
			}
		}

		// See if we are removing an old pipe.
		while (removePipe > 0) {
			pipes.pop_front()
			removePipe = removePipe - 1
		}

		// Add a new pipe if needed.
		pipetimer += delta
		if (pipetimer > pipetimerCountdown) {
			pipetimer = 0.0f
			addPipe()
		}

		landX += bird.speed * delta
		if (landX >= assets["land"].getWidth()) {
			landX = 0.0f
		}

		skyX += bird.speed / 2 * delta
		if (skyX >= assets["sky"].getWidth()) {
			skyX = 0.0f
		}

		if (!isMusicPlaying()) {
			if (score <= 10) {
				assets["music1"].play()
			}
			else if (score <= 20) {
				assets["music2"].play()
			}
			else {
				assets["music3"].play()
			}
		}

		flapTimer += delta
	}
	
	def load() {
		// Nothing.
	}

	def draw() {
		if (this.pause) {
			love.graphics.print("PAUSE", love.graphics.getWidth() / 2 - this.pauseWidth / 2, love.graphics.getHeight() / 2 - this.pauseHeight)
		}
	}

	def joystickpressed(button) {
		if (button == "start") {
			this.pause = !this.pause
			assets["point"].play()
			assets["music1"].stop()
			assets["music2"].stop()
			assets["music3"].stop()
			return
		}

		if (!this.pause) {
			if (flapTimer > flapTimerMax) {
				bird.flap(false)
				flapTimer = 0.0f
			}
		}
	}
}
