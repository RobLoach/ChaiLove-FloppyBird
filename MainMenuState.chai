class MainMenuState {
	var titlefont
	var font
	var deltaCounter
	var logo
	var textwidth
	var textheight
	var pressAnyKeyTimer

	def MainMenuState() {
		// TODO Fix the font
		this.description = "PRESS ANZ BUTTON"
		this.credits = "BZ ROB LOACH"
		this.font := assets["font-pixel"]
		this.deltaCounter = 0.0f
		this.logo := assets["logo"]
		this.textheight = this.font.getHeight()
		this.textwidth = this.font.getWidth(this.description)
		this.creditswidth = this.font.getWidth(this.credits)
	}

	def enter() {
		this.stopMusic()
		this.pressAnyKeyTimer = 1.0f
	}

	def exit() {
		// Nothing.
	}

	def draw() {
		// Logo
		var ypos = love.graphics.getHeight() / 5 + 20 * sin(this.deltaCounter * 2)
		love.graphics.draw(this.logo, love.graphics.getWidth() / 2 - this.logo.getWidth() / 2, ypos)
	
		// Credit
		love.graphics.setFont(this.font)
		ypos = love.graphics.getHeight() / 5 + this.textheight * 7 + 10 * sin(this.deltaCounter * 2)
		love.graphics.print(this.credits, love.graphics.getWidth() / 2 - this.creditswidth / 2, ypos)
		
		if (this.pressAnyKeyTimer <= 0.0f) {
			// Press Any Key
			ypos = love.graphics.getHeight() / 5 + this.textheight * 12 + 5 * sin(this.deltaCounter * 2)
			love.graphics.print(this.description, love.graphics.getWidth() / 2 - this.textwidth / 2, ypos)
		}
	}

	def update(delta) {
		this.deltaCounter += delta
		this.pressAnyKeyTimer = this.pressAnyKeyTimer - delta
	}

	def load() {
		// Nothing.
	}

	def joystickpressed(button) {
		if (this.pressAnyKeyTimer <= 0.0f) {
			// When a button is pressed, switch to the game, and restart the entities.
			states.set("game")
			reset()
		}
	}

	def stopMusic() {
		assets["music1"].stop()
		assets["music2"].stop()
		assets["music3"].stop()
	}
}
